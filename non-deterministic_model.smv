MODULE main

VAR


state : {
    IDLE,
    SYSTEM_CHECK, 
    DRIVING, 
    OBSTACLE_DETECTED, 
    STOP_SIGN_DETECTED,
    SEMAPHORE_DETECTED,
    STOPPED, 
    PARKED, 
    CHANGING_LANE, 
    TURNING
    };

input : {
    check_status, 
    no_input, 
    start, 
    brake, 
    resume, 
    park, 
    stop, 
    turn,
    change_lane, 
    change_lane_completed, 
    detect_obstacle, 
    detect_stop_sign,
    detect_red_light,
    detect_green_light,
    turn_completed
};
lane_position : {
    LEFT, 
    CENTER, 
    RIGHT, 
    NaN
    };

sensors : Sensors(input);

LTLSPEC G (state != NaN) --OK
LTLSPEC G (state = DRIVING -> !sensors.obstacle_detected) --OK
LTLSPEC G (state = DRIVING -> lane_position != NaN) --OK
LTLSPEC G (state = DRIVING -> !X(state = PARKED)) --OK
LTLSPEC G (state = OBSTACLE_DETECTED -> F (state = STOPPED)) --OK
LTLSPEC G (state = STOP_SIGN_DETECTED -> F (state = STOPPED)) --OK
LTLSPEC G (state = SEMAPHORE_DETECTED -> F (state = STOPPED)) --OK
LTLSPEC G ((state = CHANGING_LANE & input = change_lane_completed) -> X (state = DRIVING)) --OK
LTLSPEC G ((state = STOPPED & input = resume & !sensors.obstacle_detected) -> X (state = DRIVING)) --OK
LTLSPEC G ((state = STOPPED & input = resume & !sensors.stop_detected) -> X (state = DRIVING))
LTLSPEC G ((state = STOPPED & input = resume & !sensors.semaphore_detected) -> X (state = DRIVING))
LTLSPEC G ((state = TURNING & input = turn_completed & !sensors.obstacle_detected) -> X (state = DRIVING)) --OK
LTLSPEC G (input = CHANGING_LANE -> !sensors.obstacle_detected) --OK
LTLSPEC G (state = STOPPED & input = park -> X (state = PARKED)) --OK

-- specification  G state != NaN  is true
-- specification  G (state = DRIVING -> !sensors.obstacle_detected)  is true
-- specification  G (state = DRIVING -> !( X state = PARKED))  is true
-- specification  G (state = OBSTACLE_DETECTED ->  F state = STOPPED)  is true
-- specification  G (state = STOP_SIGN_DETECTED ->  F state = STOPPED)  is true
-- specification  G (state = SEMAPHORE_DETECTED ->  F state = STOPPED)  is true
-- specification  G ((state = CHANGING_LANE & input = change_lane_completed) ->  X state = DRIVING)  is true
-- specification  G (((state = STOPPED & input = resume) & !sensors.obstacle_detected) ->  X state = DRIVING)  is true
-- specification  G (((state = STOPPED & input = resume) & !sensors.stop_detected) ->  X state = DRIVING)  is true
-- specification  G (((state = STOPPED & input = resume) & !sensors.semaphore_detected) ->  X state = DRIVING)  is true
-- specification  G (((state = TURNING & input = turn_completed) & !sensors.obstacle_detected) ->  X state = DRIVING)  is true
-- specification  G (input = CHANGING_LANE -> !sensors.obstacle_detected)  is true
-- specification  G ((state = STOPPED & input = park) ->  X state = PARKED)  is true
-- specification  G (state = DRIVING -> lane_position != NaN)  is true

ASSIGN
    init(state) := IDLE;
    init(input) := no_input; 
    init(lane_position) := NaN;

    -- def of input which cases can have
    next(input) :=
        case
            next(state) = IDLE : {check_status, no_input};
            next(state) = SYSTEM_CHECK : {start};
            next(state) = DRIVING : {
                turn,
                change_lane,
                detect_stop_sign,
                detect_obstacle,
                detect_red_light,  
                no_input
            };
            next(state) = TURNING : {turn_completed};
            next(state) = CHANGING_LANE : {change_lane_completed};
            next(state) = OBSTACLE_DETECTED : {brake};
            next(state) = STOP_SIGN_DETECTED : {brake};
            next(state) = SEMAPHORE_DETECTED : {brake};
            next(state) = STOPPED :
                case
                    next(sensors.semaphore_detected) : {detect_green_light, no_input};
                    sensors.stop_detected : {resume};
                    sensors.obstacle_detected : {resume};
                    TRUE : {park, resume};
                esac;
            next(state) = PARKED : {stop, no_input};
            TRUE : no_input;
        esac;
    
    next(state) :=
        case
            state = IDLE :
                case
                    input = check_status : SYSTEM_CHECK;
                    TRUE : IDLE;
                esac;

            state = SYSTEM_CHECK :
                case
                    input = start & !sensors.obstacle_detected : DRIVING;
                    TRUE : SYSTEM_CHECK;
                esac;

            state = DRIVING :
                case
                    input = turn : TURNING;
                    input = change_lane & !sensors.obstacle_detected : CHANGING_LANE;
                    input = detect_obstacle : OBSTACLE_DETECTED;
                    input = detect_red_light : SEMAPHORE_DETECTED;
                    input = detect_stop_sign : STOP_SIGN_DETECTED;
                    TRUE : DRIVING;
                esac;

            state = TURNING :
                case
                    input = turn_completed & !sensors.obstacle_detected : DRIVING;
                    input = detect_obstacle : OBSTACLE_DETECTED;
                    TRUE : TURNING;
                esac;    
            
            state = OBSTACLE_DETECTED :
                case
                    input = brake & sensors.obstacle_detected : STOPPED;
                    TRUE : STOPPED;
                esac;

            state = STOP_SIGN_DETECTED :
                case
                    input = brake & sensors.stop_detected : STOPPED;
                    TRUE : STOPPED;
                esac;

            state = SEMAPHORE_DETECTED :
                case
                    input = brake & sensors.semaphore_detected : STOPPED;
                    TRUE : STOPPED;
                esac;

            state = CHANGING_LANE :
                case
                    input = change_lane_completed : DRIVING;
                    TRUE : CHANGING_LANE;
                esac;
            
            state = STOPPED :
                case
                    input = resume & !sensors.obstacle_detected : DRIVING;
                    input = resume & !sensors.stop_detected : DRIVING;
                    input = detect_green_light : DRIVING;
                    input = park : PARKED;
                    TRUE : STOPPED;
                esac;

            state = PARKED :
                case
                    input = stop : IDLE;
                    input = resume : STOPPED;
                    TRUE : PARKED;
                esac;
            TRUE : state;
        esac;

    next(lane_position) :=
        case
            state = SYSTEM_CHECK & input = start & !sensors.obstacle_detected :
                case
                    lane_position = NaN : RIGHT;
                    TRUE : lane_position;
                esac;

            state = CHANGING_LANE & input = change_lane_completed :
                case
                    lane_position = RIGHT : CENTER;
                    lane_position = CENTER :
                        case
                            input = change_lane : RIGHT;
                            TRUE : LEFT;
                        esac;
                    lane_position = LEFT : CENTER;
                    TRUE : lane_position;
                esac;
            TRUE : lane_position;
        esac;

MODULE Sensors(input)
VAR
    stop_detected: boolean;
    obstacle_detected : boolean;
    semaphore_detected : boolean;
ASSIGN
    init(obstacle_detected) := FALSE;
    init(semaphore_detected) := FALSE;
    init(stop_detected) := FALSE;

    next(stop_detected) := 
        case
            input = detect_stop_sign  : TRUE;
            TRUE :  FALSE;
        esac;

    next(obstacle_detected) := 
        case
            input = detect_obstacle : TRUE;
            TRUE :  FALSE;
        esac;

    next(semaphore_detected) :=
        case
            input = detect_red_light : TRUE;
            input = detect_green_light : FALSE;
            TRUE : semaphore_detected;
        esac;
